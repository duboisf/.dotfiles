emulate -RL zsh

local branch="$1"
if [[ -z "$branch" ]]; then
    echo "Usage: create-git-worktree <branch-name>" >&2
    return 1
fi
local repo
repo="$(basename "$(git rev-parse --show-toplevel)")" || return 1
local worktree_dir="../${repo}-${branch//\//-}"
git fetch origin main || return 1

if git show-ref --verify --quiet "refs/heads/$branch" ||
   git show-ref --verify --quiet "refs/remotes/origin/$branch"; then
    echo "Branch '$branch' already exists, creating worktree from it."
    git worktree add "$worktree_dir" "$branch" || return 1
else
    echo "Creating new branch '$branch' from origin/main."
    git worktree add -b "$branch" "$worktree_dir" origin/main || return 1
fi

if (( $+commands[mise] )); then
    mise trust --quiet -C "$worktree_dir"
fi
